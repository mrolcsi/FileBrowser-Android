buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}
apply plugin: 'com.android.library'

repositories {
    jcenter()
}

def makeVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    catch (ignored) {
        logger.error(ignored.printStackTrace())
        return -1;
    }
}

def makeVersionNameRelease = { ->
    try {
        def lastTag = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--tags', '--no-walk', '--max-count=1'
            standardOutput = lastTag
        }

        logger.info("*** Last Tag: " + lastTag.toString().trim())

        def lastCommits = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', lastTag.toString().trim() + '..HEAD', '--count'
            standardOutput = lastCommits
        }

        logger.info("*** Last Commits: " + lastCommits.toString().trim())

        def versionName = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = versionName
        }

        logger.info("*** Version Name: " + versionName.toString().trim())

        if (lastCommits.toString().equals(''))
            return 'v' + versionName.toString().trim()
        else
            return 'v' + versionName.toString().trim() + '.' + lastCommits.toString().trim()
    }
    catch (ignored) {
        logger.error(ignored.printStackTrace())
        return null;
    }
}

def makeVersionNameDebug = { ->
    try {

        def releaseName = makeVersionNameRelease()

        logger.info("*** Release Name: " + releaseName)

        def currentBranch = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = currentBranch
        }

        logger.info("*** Current Branch: " + currentBranch.toString().trim())

        return releaseName + '/' + currentBranch.toString().trim()
    }
    catch (ignored) {
        logger.error(ignored.printStackTrace())
        return null;
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    resourcePrefix 'browser_'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode makeVersionCode()
        versionName makeVersionNameDebug()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:appcompat-v7:23.2.1'
}
